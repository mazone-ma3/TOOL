10 'SAVE "CHR88.BAS"
20 CLEAR ,&HBCFF:MAXFILES=0:CONSOLE 0,25,0,1:SCREEN 0,0,0,1:WIDTH 80,25:DEFINT A-Z:C=7:M=33:N=3:A=0:B=16*10:EA=18*16:EB=8:O=0:P=0:BYTE=99:DIM A%(BYTE),B%(BYTE),C%(337),D%(337):II=0:CLS:DF$="1:"
30 COLOR 4:PRINT "-CHARACTER EDITOR PC-8801 VERSION 2.6 PROGRAMED BY m@3 (C) 1991/2023":COLOR 7,0:CM=0
40 GOSUB 680:LOCATE 0,1:PRINT "CLEAR(Y/N/0/1)?";:A$=INPUT$(1):PRINT A$:IF A$="Y" OR A$="y" THEN CLS 2 ELSE IF A$="1" THEN COLOR 7,3:CM=1:GOSUB 680:GOTO 40 ELSE IF A$="0" THEN COLOR 7,0:CM=0:GOTO 40 ELSE IF A$<>"N" AND A$<>"n" THEN 40
50 II=0:AD=&HBF00:RESTORE 760:GOSUB 750:II=0:RESTORE 860:AD=&HBFF0:GOSUB 750
60 CLS:KEY 1,"BOX":KEY 2,"PUT":KEY 3,"GET":KEY 4,"SAVE":KEY 5,"LOAD":KEY 6,"TURN1":KEY 7,"TURN2":KEY 8,"CHANGE":KEY 9,"ANIME":KEY 10,"CELL ":CONSOLE ,,1
70 ON KEY GOSUB 210,230,250,280,320,400,420,450,480,560:ON ERROR GOTO 720:ON STOP GOSUB 720:STOP ON
80 ON HELP GOSUB 690:HELP ON:O=0:P=0:WQ=5:LINE (EA-1,EB+16+16)-STEP(31+2,16*7+1),7,B
90 FOR I=0 TO 7:LINE (M*8,I*8+N)-(M*8+7,I*8+N+7),I,BF:NEXT I
100 FOR I=0 TO 32:LINE (I*8,0)-(I*8,16*8),7:LINE (0,(I\2)*8)-(32*8,(I\2)*8),7:NEXT
110 FOR I=16 TO 18:LOCATE 0,I:PRINT SPACE$(40):NEXT I:FOR I=1 TO 10:KEY (I) ON:NEXT I
120 I0=INP(0):I1=INP(1)
130 XX=X+((I0 AND 16)=0 AND X>0)-((I0 AND 64)=0 AND X<31):YY=Y+((I1 AND 1)=0 AND Y>0)-((I0 AND 4)=0 AND Y<15)
140 IF XX<>X OR YY<>Y THEN LINE (X*8,Y*8)-(X*8+8,Y*8+8),7,B:X=XX:Y=YY
150 LINE (X*8,Y*8)-(X*8+8,Y*8+8),1,B:LOCATE 35,0 :PRINT USING "(##-##)";X;Y
160 CC=C+((I1 AND 4)=0)-((I1 AND 16)=0)+8:CC=CC MOD 8
170 IF CC<>C THEN LINE (M*8,C*8+N)-(M*8+8,C*8+8+N),C,B:C=CC
180 LINE (M*8,C*8+N)-(M*8+8,C*8+8+N),1,B
190 IF (INP(8) AND 16)<>0 AND (INP(0) AND 1)<>0 THEN 120
200 LINE (X*8+1,Y*8+1)-(X*8+7,Y*8+7),C,BF:PSET (X+EA,Y+EB),C:GOTO 120
210 T$="<BOX>":GOSUB 660:LINE (0,0)-(32*8,16*8),C,BF:LINE (EA,EB)-STEP(31,15),C,BF:RETURN 100
220 'PUT
230 T$="<PUT>":GOSUB 660:GET@ (EA,EB)-STEP(31,15),A%:GOSUB 580:PUT@ (EA,EB),A%,PSET:PUT@ (A+O*32,B+P*16),A%,PSET:GOTO 230
240 'GET
250 T$="<GET>":GOSUB 660:GOSUB 580:PUT@ (A+O*32,B+P*16),B%,PSET:GET@ (A+O*32,B+P*16)-STEP(31,15),A%:PUT@ (EA,EB),A%,PSET
260 FOR J=0 TO 15:FOR I=0 TO 31:PCHAN=POINT(EA+I,EB+J):LINE (I*8+1,J*8+1)-(I*8+7,J*8+7),PCHAN,BF,PSET:NEXT:NEXT:RETURN 110
270 'SAVE
280 T$="<SAVE>":GOSUB 660:LOCATE 0,17:INPUT "FILE NAME";A$:IF A$="" THEN RETURN 100 ELSE IF ASC(A$)=1 THEN 280 ELSE A$=LEFT$(A$,8)+".chr"
290 AD=&HBF00:CALL AD:BSAVE DF$+A$,&H4000,&H1E00
300 RETURN 110
310 'LOAD
320 T$="<LOAD>":GOSUB 660:LOCATE 0,1:'PRINT "-FILE NAME-"
330 HJ$="":DR=VAL(DF$):QC=0:FOR I=1 TO 7:COLOR=(I,I-1):NEXT:'FOR SEC=1 TO 16:DU$=ENVIRON$(DR,1,18,SEC):FOR I=0 TO 15:F$=MID$(DU$,I*16+1,6):E$=MID$(DU$,I*16+7,3):IF E$<>"grp" THEN 350 ELSE IF F$=HJ$ THEN I=15:SEC=16:GOTO 350
340 'LOCATE (QC MOD 5)*16,(QC\5)+2:PRINT F$+"."+E$:QC=QC+1:IF QC=1 THEN HJ$=F$
350 'NEXT:IF MID$(DU$,241,1)=CHR$(255) THEN SEC=16:NEXT ELSE NEXT
360 GOSUB 660:LOCATE 0,17:INPUT "FILE NAME";A$:IF A$="" THEN CLS:GOSUB 680:RETURN 100 ELSE IF ASC(A$)=1 THEN 320 ELSE A$=LEFT$(A$,8)+".chr"
370 BLOAD DF$+A$,&H4000:GOSUB 680:AD=&HBF50:CALL AD
380 CLS:RETURN 110
390 'HANTEN
400 T$="<HANTEN>":GOSUB 660:LINE (X*8,Y*8)-STEP(8,8),7,B:FOR I=0 TO 15:GET@ (EA+I,EB)-STEP(0,15),A%:GET (EA+31-I,EB)-STEP(0,15),B%:PUT@ (EA+31-I,EB),A%,PSET:PUT@ (EA+I,EB),B%,PSET
410 GET@ (I*8+1,1)-STEP(6,127),C%:GET@ (256-1-6-I*8,1)-STEP(6,127),D%:PUT@ (256-1-6-I*8,1),C%,PSET:PUT@ (I*8+1,1),D%,PSET:NEXT:RETURN 110
420 T$="<HANTEN>":GOSUB 660:LINE (X*8,Y*8)-STEP(8,8),7,B:FOR I=0 TO 7:GET@ (EA,EB+I)-STEP(31,0),A%:GET (EA,EB+15-I)-STEP(31,0),B%:PUT@ (EA,EB+15-I),A%,PSET:PUT@ (EA,EB+I),B%,PSET
430 GET@ (1,I*8+1)-STEP(255,6),C%:GET@ (1,128-1-6-I*8)-STEP(255,6),D%:PUT@ (1,128-1-6-I*8),C%,PSET:PUT@ (1,I*8+1),D%,PSET:NEXT:RETURN 110
440 'CHANGE
450 T$="<CHANGE>":GOSUB 660:GOSUB 580:PUT@ (A+O*32,B+P*16),B%,PSET
460 QA=O:QB=P:GOSUB 580:IF QA=O AND QB=P THEN PUT@ (A+O*32,B+P*16),B%,PSET:GOTO 450 ELSE GET@ (A+QA*32,B+QB*16)-STEP(31,15),A%:PUT@ (A+QA*32,B+QB*16),B%,PSET:PUT@ (A+O*32,B+P*16),A%,PSET:GOTO 450
470 'ANIME
480 T$="<ANIME>":GOSUB 660:LOCATE 0,17:INPUT "PATTERN(1-7)";WQ:IF WQ>7 THEN WQ=7 ELSE IF WQ=0 THEN RETURN 110
490 LOCATE 0,17:GOSUB 670:INPUT "1-GET 2-MOVE";Q:IF Q=2 THEN 510 ELSE IF Q>2 THEN 480 ELSE IF Q=0 THEN RETURN 110
500 LOCATE 0,17:PRINT "GET PATTERN !!!!!!":FOR I=1 TO WQ:LOCATE 0,18:PRINT "STEP-";I:GOSUB 580:PUT@ (A+O*32,B+P*16),B%,PSET:PUT@ (EA,EB+I*16+16+1),B%,PSET:NEXT I
510 LOCATE 0,17:WAI=0:GOSUB 670:INPUT "WAIT TIME(0-1000)";WAI :IF WAI >1000 OR WAI <0 THEN 510
520 LOCATE 0,18:LOOP=0:GOSUB 670:INPUT "LOOP TIME(0-1000)";LOOP:IF LOOP>1000 OR LOOP<0 THEN 520
530 FOR K=1 TO LOOP:FOR I=1 TO WQ:GET@ (EA,EB+I*16+16+1)-STEP(31,15),B%:PUT@ (EA,EB+16),B%,PSET:FOR J=0 TO WAI :NEXT J,I,K
540 RETURN 110
550 'CELL
560 T$="<CELL>":GOSUB 660:FOR J=0 TO 15:FOR I=0 TO 31:PC=POINT(EA+I,EB+J):PC=-(PC=0)*7:PSET (EA+I,EB+J),PC,PSET:LINE (I*8+1,J*8+1)-(I*8+7,J*8+7),PC,BF,PSET:NEXT:NEXT:RETURN 110
570 'KEY SENSE SUB
580 GOSUB 670:GET@ (A+O*32,B+P*16)-STEP(31,15),B%
590 'KEY SENSE
600 I0=INP(0):I1=INP(1)
610 OO=O+((I0 AND 16)=0 AND O>0)-((I0 AND 64)=0 AND O<19):PP=P+((I1 AND 1)=0 AND P>0)-((I0 AND 4)=0 AND P< 1)
620 IF OO<>O OR PP<>P THEN PUT@ (A+O*32,B+P*16),B%,PSET:O=OO:P=PP:GET@ (A+O*32,B+P*16)-STEP(31,15),B%
630 LINE (O*32+A,P*16+B)-STEP(31,15),1,B,,PSET:IF (INP(0) AND 1)=0 OR (INP(8) AND 16)=0 OR (INP(9) AND 64)=0 THEN 631 ELSE 640
631 IF (INP(0) AND 1)=0 OR (INP(8) AND 16)=0 OR (INP(9) AND 64)=0 THEN 631 ELSE RETURN
640 IF INP(1) AND 128 THEN 600 ELSE PUT@ (A+O*32,B+P*16),B%,PSET:GOSUB 670:RETURN 650
650 GOSUB 670:RETURN 110
660 GOSUB 710:LOCATE 0,16:PRINT T$
670 A$="":IF INKEY$<>"" THEN 670 ELSE RETURN
680 FOR I=0 TO 7:COLOR=(I,I):NEXT:IF CM=1 THEN COLOR=(0,3):COLOR=(3,0):RETURN ELSE RETURN
690 FOR J=0 TO 1 :FOR I=0 TO 19:LOCATE I*4,J*2+20:PRINT I+J*10;:NEXT I,J:ON HELP GOSUB 700:RETURN
700 FOR J=0 TO 1 :FOR I=0 TO 19:LOCATE I*4,J*2+20:PRINT "    " :NEXT I,J:ON HELP GOSUB 690:RETURN
710 FOR I=1 TO 10:KEY (I) OFF:NEXT I:RETURN
720 '
730 GOSUB 680:CONSOLE ,,0:GOSUB 710:KEY 1,"load "+CHR$(34):KEY 2,"auto ":KEY 3,"go to ":KEY 4,"list ":KEY 5,"run"+CHR$(13):KEY 6,"save "+CHR$(34):KEY 7,"files "+CHR$(13):KEY 8,"run"+CHR$(34)+"RUN"+CHR$(13):KEY 9,"edit ."+CHR$(13):KEY 10,"cont"+CHR$(13)
740 ON ERROR GOTO 0:END
750 READ A$:IF A$="*" THEN RETURN ELSE POKE AD+II,VAL("&H"+A$):II=II+1:GOTO 750
760 DATA F3,3A,C2,E6,F6,02,D3,31,2A,F0,BF,ED,5B,F4,BF,ED
770 DATA 4B,F2,BF,D5,C5,D3,5C,1A,D3,5F,77,23,13,10,F6,C1
780 DATA D1,D5,C5,D3,5D,1A,D3,5F,77,23,13,10,F6,C1,D1,D5
790 DATA C5,D3,5E,1A,D3,5F,77,23,13,10,F6,C1,D1,E5,21,50
800 DATA 00,19,EB,E1,0D,20,CC,3A,C2,E6,D3,31,FB,C9,00,00
810 DATA F3,3A,C2,E6,F6,02,D3,31,2A,F0,BF,ED,5B,F4,BF,ED
820 DATA 4B,F2,BF,C5,D5,48,06,00,C5,D3,5C,ED,B0,00,00,C1
830 DATA D1,D5,C5,D3,5D,ED,B0,00,00,C1,D1,D5,D3,5E,ED,B0
840 DATA 00,00,D1,E5,21,50,00,19,EB,E1,C1,0D,20,D5,3A,C2
850 DATA E6,D3,31,D3,5F,FB,C9,*
860 DATA 00,40,20,50,00,F2,*
