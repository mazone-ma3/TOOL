10 '    SAVE "1:CHR88.BAS"
11 '    SAVE "CHR98_.BAS"
12 '    SAVE "1:CHR88.ASC",A
13 '    SAVE "CHR98_.ASC",A
15 ' DELETE 280-300:DELETE 320-380:DELETE 750-860
20 CLEAR        :MAXFILES=0:CONSOLE 1,25,0,1:WIDTH 80,25:DEFINT A-Z:CLS:LX=640:LY=200:HD=1:IO=&HE0:INPUT "LINE(1-200 2-400)";I:INPUT "X";SX:INPUT "Y";SY:SX=SX-(SX=0)*32:SY=SY-(SY=0)*(16-16*(I=2)):IF I=2 THEN LY=400
21 CLS 1:SCREEN -(LY=400)*3,0,0,1:GX=0:GY=16*10*(LY\200):WX=256\SX:WY=WX:IF WY*SY>GY THEN WY=GY\SY:WX=WY
22 ZX=8:ZY=7*(LY\200):CR=15:KC=CR:RC=0:RX=280:RY=3*8:EX=37*8:EY=8*(LY\200):IR=CR:TX=45:TY=1:UX=0:UY=17:BYTE=(((SX+7)\8)*SY*4+4)\2+1:BETE=(((WX+7)\8)*SY*WY*4+4)\2+1:BETE=1000:DIM A%(BYTE),B%(BYTE),C%(BETE),D%(BETE):II=0:DF$="":WA=3
23 IF IO<>0 THEN COLOR ,,,,2:GOSUB 681:GX=0:GY=0
30 LOCATE 0,0:COLOR 4:PRINT "-CHARACTER EDITOR PC"+CHR$(ASC("8")-(IO<>0))+"801 VERSION 3.2 PROGRAMED BY m@3 (C) 1991/1992/2023":COLOR 7,0:CM=0
40 GOSUB 680:LOCATE 0,1:PRINT "CLEAR(Y/N/0/1)?";:A$=INPUT$(1):PRINT A$:IF A$="Y" OR A$="y" THEN CLS 2:SCREEN ,,0,1:CLS 2 ELSE IF A$="1" THEN COLOR 7,3:CM=1:GOSUB 680:GOTO 40 ELSE IF A$="0" THEN COLOR 7,0:CM=0:GOTO 40 ELSE IF A$<>"N" AND A$<>"n" THEN 40
50 SCREEN ,,0,1:IF IO=0 THEN II=0:AD=&HBF00:RESTORE 760:GOSUB 750:II=0:RESTORE 860:AD=&HBFF0:GOSUB 750
60 LOCATE 0,1:PRINT SPC(79):KEY 1,"BOX":KEY 2,"PUT":KEY 3,"GET":KEY 4,"SAVE":KEY 5,"LOAD":KEY 6,"TURN1":KEY 7,"TURN2":KEY 8,"CHANGE":KEY 9,"ANIME":KEY 10,"CELL  ":CONSOLE 1,25,1
70 ON KEY GOSUB 210,230,250,280,320,400,420,450,480,560:ON ERROR GOTO 720:ON STOP GOSUB 720:STOP ON
80 ON HELP GOSUB 690:HELP ON:O=0:P=0:WQ=5:'LINE (EX-1,EY+SY*2)-STEP(SX+1,SY*7+1),7,B
90 FOR I=0 TO IR:LINE (RX,I*8+RY)-STEP(8-1,8-1),I,BF:NEXT
100 LINE (ZX-1,ZY-1)-STEP(SX*WX+2,SY*WY+2),1,B:FOR I=0 TO SX:LINE (I*WX+ZX,ZY)-STEP(0,SY*WY),KC:NEXT:FOR I=0 TO SY:LINE (ZX,I*WY+ZY)-STEP(SX*WX,0),KC:NEXT
110 SCREEN ,,0,1:FOR I=16 TO 18:LOCATE 0,I:PRINT SPACE$(40):NEXT  :FOR I=1 TO 10:KEY (I) ON:NEXT
120 I0=INP(IO):I1=INP(IO+1)
130 XX=X+((I0 AND 16)=0 AND X>0)-((I0 AND 64)=0 AND X<SX-1):YY=Y+((I1 AND 1)=0 AND Y>0)-((I0 AND 4)=0 AND Y<SY-1)
140 IF XX<>X OR YY<>Y THEN LINE (ZX+X*WX,ZY+Y*WY)-STEP(WX,WY),KC,B:X=XX:Y=YY
150 LINE (ZX+X*WX,ZY+Y*WY)-STEP(WX,WY),RND(1)*5+1,B:LOCATE TX,TY:PRINT USING "(##-##)##";X;Y
160 CC=CR+((I1 AND 4)=0)-((I1 AND 16)=0)+IR+1:CC=CC MOD (IR+1)
170 IF CC<>CR THEN LINE (RX,CR*8+RY)-STEP(8-1,8-1),CR,B:CR=CC
180 LINE (RX,CR*8+RY)-STEP(8-1,8-1),RND(1)*5+1,B
190 GOSUB 642:ON A GOTO 191,192:GOTO 200
191 A=CR:GOSUB 202:GOTO 200
192 A=RC:GOSUB 202
200 IF INP(IO+8) AND 64 THEN FOR IW=0 TO WA*100:NEXT ELSE WE=INP(IO+11):WA=WA+((WE AND 2)=0)-((WE AND 1)=0)+32:WA=WA MOD 32:LOCATE TX,TY+1:PRINT USING"WAIT=###";WA:GOTO 200
201 GOTO 120
202 LINE (ZX+X*WX+1,ZY+Y*WY+1)-STEP(WX-2,WY-2),A,BF:PSET (X+EX,Y+EY),A:RETURN
205 'BOX
210 T$="<BOX>":GOSUB 660:LINE (EX,EY)-STEP(SX-1,SY-1),CR,BF:A=CR:GOSUB 211:RETURN 100
211 LINE (ZX,ZY)-STEP(SX*WX,SY*WY),A,BF:RETURN
220 'PUT
230 T$="<PUT>":GOSUB 660:GET@ (EX,EY)-STEP(SX-1,SY-1),A%
231 GOSUB 580:PUT@ (GX+O*SX,GY+P*SY),A%,PSET:GOTO 231
240 'GET
250 T$="<GET>":GOSUB 660:GOSUB 580:PUT@ (GX+O*SX,GY+P*SY),B%,PSET:GET@ (GX+O*SX,GY+P*SY)-STEP(SX-1,SY-1),A%:SCREEN ,,0,1:PUT@ (EX,EY),A%,PSET:A=KC:GOSUB 211
260 FOR J=0 TO SY-1:FOR I=0 TO SX-1:GP=POINT(EX+I,EY+J):IF GP<>KC THEN LINE (ZX+I*WX+1,ZY+J*WY+1)-STEP(WX-2,WY-2),GP,BF
261 NEXT:NEXT:RETURN 110
270 'SAVE
280 T$="<SAVE>":GOSUB 660:LOCATE UX,UY+1:INPUT "FILE NAME";A$:IF A$="" THEN RETURN 100 ELSE IF ASC(A$)=1 THEN 280 ELSE A$=LEFT$(A$,8)+".gr"
290 IF IO=0 THEN AD=&HBF00:CALL AD:BSAVE DF$+A$+"p",&H4000,&H1E00:RETURN 110 ELSE GOSUB 681:FOR I=0 TO 3:DEF SEG=&HA800+&H800*I-(I=3)*&H2000:F$=DF$+A$+MID$("brgi",I+1,1):IF ((LY\200)=1) THEN BSAVE F$,0,&H4000 ELSE BSAVE F$,0,&H8000
300 NEXT:RETURN 110
310 'LOAD
320 T$="<LOAD>":GOSUB 660:'LOCATE 0,1:PRINT "-FILE NAME-"
330 HJ$="":DR=VAL(DF$):QC=0:'FOR I=1 TO 7:COLOR=(I,I):NEXT:'FOR SEC=1 TO 16-(HD<>0)*10*0:DU$=DSKI$(DR,-(HD=0),18-(HD<>0)*17,SEC):FOR I=0 TO 15:F$=MID$(DU$,I*16+1,6):E$=MID$(DU$,I*16+7,2):IF E$<>"gr" THEN 350 ELSE IF F$=HJ$ THEN I=15:SEC=16:GOTO 350
340 'LOCATE (QC MOD 5)*16,(QC\5)+2:PRINT F$+"."+E$+"p":QC=QC+1:IF QC=1 THEN HJ$=F$
350 'NEXT:IF MID$(DU$,241,1)=CHR$(255) THEN SEC=16:NEXT ELSE NEXT
360 GOSUB 660:LOCATE UX,UY+1:INPUT "FILE NAME";A$:IF A$="" THEN CLS:GOSUB 680:RETURN 100 ELSE IF ASC(A$)=1 THEN 320 ELSE A$=LEFT$(A$,8)+".gr"
370 GOSUB 680:IF IO=0 THEN BLOAD DF$+A$+"p",&H4000:AD=&HBF50:CALL AD ELSE GOSUB 681:FOR I=0 TO 3:DEF SEG=&HA800+&H800*I-(I=3)*&H2000:BLOAD DF$+A$+MID$("brgi",I+1,1),0:NEXT
380 CLS:RETURN 110
390 'HANTEN
400 T$="<YOKO-HANTEN>":GOSUB 660:LINE (X*WX+ZX,Y*WY+ZY)-STEP(WX,WY),KC,B:FOR I=0 TO SX\2-1:GET@ (EX+I,EY)-STEP(0,SY-1),A%:GET@ (EX+SX-1-I,EY)-STEP(0,SY-1),B%:PUT@ (EX+SX-1-I,EY),A%,PSET:PUT@ (EX+I,EY),B%,PSET
410 K=I*WX+1+ZX:L=WX*SX+1-WX-I*WX+ZX:GET@ (K,1+ZY)-STEP(WX-2,SY*WY-1),C%:GET@ (L,1+ZY)-STEP(WX-2,SY*WY-1),D%:PUT@ (L,1+ZY),C%,PSET:PUT@ (K,1+ZY),D%,PSET:NEXT:RETURN 110
420 T$="<TATE-HANTEN>":GOSUB 660:LINE (X*WX+ZX,Y*WY+ZY)-STEP(WX,WY),KC,B:FOR I=0 TO SY\2-1:GET@ (EX,EY+I)-STEP(SX-1,0),A%:GET@ (EX,EY+SY-1-I)-STEP(SX-1,0),B%:PUT@ (EX,EY+SY-1-I),A%,PSET:PUT@ (EX,EY+I),B%,PSET
430 K=I*WY+1+ZY:L=SY*WY+1-WY-I*WY+ZY:GET@ (1+ZX,K)-STEP(SX*WX-1,WY-2),C%:GET@ (1+ZX,L)-STEP(SX*WX-1,WY-2),D%:PUT@ (1+ZX,L),C%,PSET:PUT@ (1+ZX,K),D%,PSET:NEXT:RETURN 110
440 'CHANGE
450 T$="<CHANGE>":GOSUB 660:GOSUB 580:PUT@ (GX+O*SX,GY+P*SY),B%,PSET
460 QA=O:QB=P:GOSUB 580:IF QA=O AND QB=P THEN PUT@ (GX+O*SX,GY+P*SY),B%,PSET:GOTO 450 ELSE GET@ (GX+QA*SX,GY+QB*SY)-STEP(SX-1,SY-1),A%:PUT@ (GX+QA*SX,GY+QB*SY),B%,PSET:PUT@ (GX+O*SX,GY+P*SY),A%,PSET:GOTO 450
470 'ANIME
480 T$="<ANIME>":GOSUB 660:LOCATE UX,UY+1:INPUT "PATTERN(1-14)";WQ:IF WQ>14 THEN WQ=12 ELSE IF WQ=0 THEN RETURN 110
490 LOCATE UX,UY+1:GOSUB 670:INPUT "1-GET 2-MOVE";Q:IF Q=2 THEN 510 ELSE IF Q>2 THEN 480 ELSE IF Q=0 THEN RETURN 110
500 LOCATE UX,UY+1:PRINT "GET PATTERN !!!!!!":FOR J=1 TO WQ:LOCATE 0,18:PRINT "STEP-";J:FOR I=0 TO 1:I=-(INP(IO+1)=255):NEXT:GOSUB 580:PUT@ (GX+O*SX,GY+P*SY),B%,PSET:SCREEN ,,0,1:PUT@ (EX+(J MOD 7)*SX,EY+(J \ 7)*SY+SY+1),B%,PSET:NEXT
510 LOCATE UX,UY+1:WAI=0:GOSUB 670:INPUT "WAIT TIME(0-1000)";WAI :IF WAI >1000 OR WAI <0 THEN 510
520 LOCATE UX,UY+2:LOOP=0:GOSUB 670:INPUT "LOOP TIME(0-1000)";LOOP:IF LOOP>1000 OR LOOP<0 THEN 520
530 FOR K=1 TO LOOP:FOR I=1 TO WQ:GET@ (EX+(I MOD 7)*SX,EY+(I \ 7)*SY+SY+1)-STEP(SX-1,SY-1),B%:PUT@ (EX,EY+SY),B%,PSET:FOR J=0 TO WAI :NEXT J,I,K
540 RETURN 110
550 'SEL
560 T$="<CELL>":GOSUB 660:FOR J=0 TO SY-1:FOR I=0 TO SX-1:PC=POINT(EX+I,EY+J):PC=-(PC=0)*7:PSET (EX+I,EY+J),PC     :LINE (I*WX+1+ZX,J*WY+1+ZY)-STEP(WX-2,WY-2),PC,BF     :NEXT:NEXT:RETURN 110
570 'AREA SUB
580 GOSUB 681
581 GOSUB 670:GET@ (GX+O*SX,GY+P*SY)-STEP(SX-1,SY-1),B%
590 'AREA CURSOR MOVE
600 I0=INP(IO):I1=INP(IO+1)
610 OO=O+((I0 AND 16)=0 AND O>0)-((I0 AND 64)=0 AND O+1<(LX\SX)):PP=P+((I1 AND 1)=0 AND P>0)-((I0 AND 4)=0 AND GY+P*SY+SY*2-1<LY)
620 IF OO<>O OR PP<>P THEN PUT@ (GX+O*SX,GY+P*SY),B%,PSET:O=OO:P=PP:GET@ (GX+O*SX,GY+P*SY)-STEP(SX-1,SY-1),B%
630 LINE (O*SX+GX,P*SY+GY)-STEP(SX-1,SY-1),RND(1)*5+1,B      :GOSUB 642:IF A<>1 THEN 640 ELSE BEEP:GOSUB 670
631 GOSUB 642:IF A<>0 THEN 631 ELSE RETURN
640 IF A=0 THEN GOSUB 645:GOTO 600 ELSE PUT@ (GX+O*SX,GY+P*SY),B%,PSET:GOSUB 670:RETURN 650
641 '
642 B=INP(IO+5):C=INP(IO+9):A=0:IF (B AND 1)=0 OR (C AND 64)=0 THEN A=1 ELSE IF (B AND 4)=0 OR (C AND 128)=0 THEN A=2 ELSE IF (INP(IO+1) AND 128)=0 THEN A=3
643 RETURN
644 '
645 IF INP(IO+8) AND 64 THEN FOR IW=0 TO WA*100:NEXT:RETURN ELSE WE=INP(IO+11):WA=WA+((WE AND 2)=0 AND WA>0)-((WE AND 1)=0 AND WA<32):GOTO 645
650 GOSUB 670:RETURN 110
660 GOSUB 710:LOCATE UX,UY:COLOR 4:PRINT T$:COLOR 7
670 A$="":IF INKEY$<>"" THEN 670 ELSE RETURN
680 RETURN:FOR I=1 TO 7:COLOR=(I,I):NEXT:IF CM=1 THEN COLOR=(0,3):COLOR=(3,0):RETURN ELSE RETURN
681 IF IO<>0 THEN IF LY=400 THEN SCREEN ,,1,33 ELSE SCREEN ,,2,33
682 RETURN
690 'FOR J=0 TO 1 :FOR I=0 TO 19:LOCATE I*4,J*2+20:PRINT I+J*10;:NEXT I,J:ON HELP GOSUB 700:RETURN
700 'FOR J=0 TO 1 :FOR I=0 TO 19:LOCATE I*4,J*2+20:PRINT "    " :NEXT I,J:ON HELP GOSUB 690:RETURN
701 RESTORE 708
702 'READ A$:IF A$<>"*" THEN PRINT A$:GOTO 702
708 DATA WINDOW OPEN!,*
709 RETURN
710 FOR I=1 TO 10:KEY (I) OFF:NEXT I:RETURN
720 '
730 GOSUB 680:CONSOLE 0,,0:GOSUB 710:KEY 1,"load "+CHR$(34):KEY 2,"auto ":KEY 3,"go to ":KEY 4,"list ":KEY 5,"run"+CHR$(13):KEY 6,"save "+CHR$(34):KEY 7,"files "+CHR$(13):KEY 8,"run"+CHR$(34)+"RUN"+CHR$(13):KEY 9,"edit ."+CHR$(13):KEY 10,"cont"+CHR$(13)
740 ' FOR I=1 TO 7:COLOR=(I,I):NEXT:ON ERROR GOTO 0:END
741 ON ERROR GOTO 0:END
750 READ A$:IF A$<>"*" THEN POKE AD+II,VAL("&H"+A$):II=II+1:GOTO 750
760 DATA F3,3A,C2,E6,F6,02,D3,31,2A,F0,BF,ED,5B,F4,BF,ED
770 DATA 4B,F2,BF,D5,C5,D3,5C,1A,D3,5F,77,23,13,10,F6,C1
780 DATA D1,D5,C5,D3,5D,1A,D3,5F,77,23,13,10,F6,C1,D1,D5
790 DATA C5,D3,5E,1A,D3,5F,77,23,13,10,F6,C1,D1,E5,21,50
800 DATA 00,19,EB,E1,0D,20,CC,3A,C2,E6,D3,31,FB,C9,00,00
810 DATA F3,3A,C2,E6,F6,02,D3,31,2A,F0,BF,ED,5B,F4,BF,ED
820 DATA 4B,F2,BF,C5,D5,48,06,00,C5,D3,5C,ED,B0,00,00,C1
830 DATA D1,D5,C5,D3,5D,ED,B0,00,00,C1,D1,D5,D3,5E,ED,B0
840 DATA 00,00,D1,E5,21,50,00,19,EB,E1,C1,0D,20,D5,3A,C2
850 DATA E6,D3,31,D3,5F,FB,C9,*
860 DATA 00,40,20,50,00,F2,*
861 '    RAM   Y  X  VRAM
870 RETURN
10000 F$="AHO"
20000  DEF SEG=&HA800:A$="BLU":GOSUB *SAV
30000  DEF SEG=&HB000:A$="RED":GOSUB *SAV
40000  DEF SEG=&HB800:A$="GRE":GOSUB *SAV
50000 END
60000 *SAV
60070 FOR I=0 TO 255: POKE I+J,255:NEXT:J=J+256
60080 RETURN
